plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17

    withJavadocJar()
    withSourcesJar()
}

group 'com.satergo'
version '0.0.1'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
        content { includeGroup 'org.scorexfoundation' }
    }
}

dependencies {
    api ('org.scorexfoundation:sigma-state_2.13:5.0.14') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15to18'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    api 'org.jspecify:jspecify:1.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.2'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'com.satergo.ergonnection')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.addBooleanOption('html5', true)
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('Xdoclint:-missing', true)
}